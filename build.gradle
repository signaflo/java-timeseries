plugins {
    id "com.github.johnrengelman.shadow" version "2.0.3"
    id "java-library"
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'jacoco'

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }
    repositories {
        jcenter()
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project.properties.ossrhUsername,
                            password: project.properties.ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: project.properties.ossrhUsername,
                            password: project.properties.ossrhPassword)
                }

                pom.project {
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Time Series Analysis in Java'
                    url 'https://github.com/jrachiele/java-timeseries'

                    scm {
                        connection 'scm:git:git://github.com/signaflo/java-timeseries.git'
                        developerConnection 'scm:git:ssh://github.com/signaflo/java-timeseries.git'
                        url 'https://github.com/signaflo/java-timeseries'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            name 'Jacob Rachiele'
                            email 'jrachiele@utexas.edu'
                        }
                    }
                }
            }
        }
    }

    shadowJar {
        configurations = [project.configurations.getByName("runtimeClasspath")]
        archiveName = "signal.jar"
    }

    dependencies {

        //apt 'org.immutables:value:2.4.4'
        //compileOnly 'org.immutables:value:2.4.4:annotations'
        compileOnly 'org.projectlombok:lombok:1.18.0'

        implementation 'com.google.guava:guava:21.0'
        implementation 'org.ejml:all:0.30'
        implementation 'org.knowm.xchart:xchart:3.2.2'
        implementation 'org.slf4j:slf4j-api:1.7.25'

        testImplementation 'com.google.guava:guava-testlib:21.0'
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testRuntimeOnly 'org.slf4j:slf4j-nop:1.7.25'
    }
}


